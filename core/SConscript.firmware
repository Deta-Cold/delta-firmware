# pylint: disable=E0602
# fmt: off

import os
import tools

BITCOIN_ONLY = ARGUMENTS.get('BITCOIN_ONLY', '0')
PRODUCTION = ARGUMENTS.get('PRODUCTION', '0') == '1'
BOOTLOADER_QA = ARGUMENTS.get('BOOTLOADER_QA', '0') == '1'
EVERYTHING = BITCOIN_ONLY != '1'
detahard_MODEL = ARGUMENTS.get('detahard_MODEL', 'T')
DMA2D = detahard_MODEL in ('T', )
CMAKELISTS = int(ARGUMENTS.get('CMAKELISTS', 0))


FEATURE_FLAGS = {
    "RDI": True,
    "SECP256K1_ZKP": True,  # required for detahard.crypto.curve.bip340 (BIP340/Taproot)
    "SYSTEM_VIEW": False,
}

FEATURES_WANTED = ["input", "sbu", "sd_card", "rgb_led"]

CCFLAGS_MOD = ''
CPPPATH_MOD = []
CPPDEFINES_MOD = []
SOURCE_MOD = []

PYOPT = ARGUMENTS.get('PYOPT', '1')
FROZEN = True

if detahard_MODEL in ('1', 'R'):
    FONT_NORMAL='Font_PixelOperator_Regular_8'
    FONT_DEMIBOLD=None
    FONT_BOLD='Font_PixelOperator_Bold_8'
    FONT_MONO='Font_PixelOperatorMono_Regular_8'
if detahard_MODEL in ('T', ):
    FONT_NORMAL='Font_TTHoves_Regular_21'
    FONT_DEMIBOLD='Font_TTHoves_DemiBold_21'
    FONT_BOLD='Font_TTHoves_Bold_17'
    FONT_MONO='Font_RobotoMono_Medium_20'

# moddetahardconfig
CPPPATH_MOD += [
    'embed/extmod/moddetahardconfig',
    'vendor/detahard-storage',
]
SOURCE_MOD += [
    'embed/extmod/moddetahardconfig/moddetahardconfig.c',
    'vendor/detahard-storage/norcow.c',
    'vendor/detahard-storage/storage.c',
]

# moddetahardcrypto
CCFLAGS_MOD += '-Wno-sequence-point '
CPPPATH_MOD += [
    'vendor/detahard-crypto',
]
CPPDEFINES_MOD += [
    'AES_128',
    'AES_192',
    ('USE_BIP32_CACHE', '0'),
    ('USE_KECCAK', '1'),
    ('USE_ETHEREUM', '1' if EVERYTHING else '0'),
    ('USE_MONERO', '1' if EVERYTHING else '0'),
    ('USE_CARDANO', '1' if EVERYTHING else '0'),
    ('USE_NEM', '1' if EVERYTHING else '0'),
    ('USE_EOS', '1' if EVERYTHING else '0'),
]
SOURCE_MOD += [
    'embed/extmod/detahardobj.c',
    'embed/extmod/moddetahardcrypto/crc.c',
    'embed/extmod/moddetahardcrypto/moddetahardcrypto.c',
    'embed/extmod/moddetahardcrypto/rand.c',
    'vendor/detahard-crypto/address.c',
    'vendor/detahard-crypto/aes/aes_modes.c',
    'vendor/detahard-crypto/aes/aescrypt.c',
    'vendor/detahard-crypto/aes/aeskey.c',
    'vendor/detahard-crypto/aes/aestab.c',
    'vendor/detahard-crypto/base32.c',
    'vendor/detahard-crypto/base58.c',
    'vendor/detahard-crypto/bignum.c',
    'vendor/detahard-crypto/bip32.c',
    'vendor/detahard-crypto/bip39.c',
    'vendor/detahard-crypto/bip39_english.c',
    'vendor/detahard-crypto/blake256.c',
    'vendor/detahard-crypto/blake2b.c',
    'vendor/detahard-crypto/blake2s.c',
    'vendor/detahard-crypto/chacha20poly1305/chacha20poly1305.c',
    'vendor/detahard-crypto/chacha20poly1305/chacha_merged.c',
    'vendor/detahard-crypto/chacha20poly1305/poly1305-donna.c',
    'vendor/detahard-crypto/chacha20poly1305/rfc7539.c',
    'vendor/detahard-crypto/chacha_drbg.c',
    'vendor/detahard-crypto/curves.c',
    'vendor/detahard-crypto/ecdsa.c',
    'vendor/detahard-crypto/ed25519-donna/curve25519-donna-32bit.c',
    'vendor/detahard-crypto/ed25519-donna/curve25519-donna-helpers.c',
    'vendor/detahard-crypto/ed25519-donna/curve25519-donna-scalarmult-base.c',
    'vendor/detahard-crypto/ed25519-donna/ed25519-donna-32bit-tables.c',
    'vendor/detahard-crypto/ed25519-donna/ed25519-donna-basepoint-table.c',
    'vendor/detahard-crypto/ed25519-donna/ed25519-donna-impl-base.c',
    'vendor/detahard-crypto/ed25519-donna/ed25519-keccak.c',
    'vendor/detahard-crypto/ed25519-donna/ed25519-sha3.c',
    'vendor/detahard-crypto/ed25519-donna/ed25519.c',
    'vendor/detahard-crypto/ed25519-donna/modm-donna-32bit.c',
    'vendor/detahard-crypto/groestl.c',
    'vendor/detahard-crypto/hasher.c',
    'vendor/detahard-crypto/hmac.c',
    'vendor/detahard-crypto/hmac_drbg.c',
    'vendor/detahard-crypto/memzero.c',
    'vendor/detahard-crypto/nem.c',
    'vendor/detahard-crypto/nist256p1.c',
    'vendor/detahard-crypto/pbkdf2.c',
    'vendor/detahard-crypto/rand.c',
    'vendor/detahard-crypto/rfc6979.c',
    'vendor/detahard-crypto/ripemd160.c',
    'vendor/detahard-crypto/secp256k1.c',
    'vendor/detahard-crypto/segwit_addr.c',
    'vendor/detahard-crypto/sha2.c',
    'vendor/detahard-crypto/sha3.c',
    'vendor/detahard-crypto/shamir.c',
    'vendor/detahard-crypto/slip39.c',
    'vendor/detahard-crypto/slip39_english.c',
]
if EVERYTHING:
    SOURCE_MOD += [
        'vendor/detahard-crypto/cardano.c',
        'vendor/detahard-crypto/monero/base58.c',
        'vendor/detahard-crypto/monero/serialize.c',
        'vendor/detahard-crypto/monero/xmr.c',
    ]

# libsecp256k1-zkp
if FEATURE_FLAGS["SECP256K1_ZKP"]:
    CPPPATH_MOD += [
        'vendor/secp256k1-zkp',
        'vendor/secp256k1-zkp/src',
        'vendor/secp256k1-zkp/include',
    ]
    CPPDEFINES_MOD += [
        'USE_SECP256K1_ZKP',
        'USE_SECP256K1_ZKP_ECDSA',
        ('SECP256K1_CONTEXT_SIZE', '180'),
        'USE_ASM_ARM',
        'USE_EXTERNAL_ASM',
        'USE_EXTERNAL_DEFAULT_CALLBACKS',
        ('ECMULT_GEN_PREC_BITS', '2'),
        ('ECMULT_WINDOW_SIZE', '8'),
        'ENABLE_MODULE_GENERATOR',
        'ENABLE_MODULE_RECOVERY',
        'ENABLE_MODULE_SCHNORRSIG',
        'ENABLE_MODULE_EXTRAKEYS',
    ]
    SOURCE_MOD_SECP256K1_ZKP = [
        'vendor/secp256k1-zkp/src/secp256k1.c',
        'vendor/secp256k1-zkp/src/precomputed_ecmult.c',
        'vendor/secp256k1-zkp/src/precomputed_ecmult_gen.c',
        'vendor/secp256k1-zkp/src/asm/field_10x26_arm.s'
    ]
    SOURCE_MOD += [
        'vendor/detahard-crypto/zkp_context.c',
        'vendor/detahard-crypto/zkp_ecdsa.c',
        'vendor/detahard-crypto/zkp_bip340.c',
    ]

# moddetahardio
SOURCE_MOD += [
    'embed/extmod/moddetahardio/moddetahardio.c',
]

# moddetahardui
CPPPATH_MOD += [
        'vendor/micropython/lib/uzlib',
]
SOURCE_MOD += [
    'embed/lib/buffers.c',
    'embed/lib/colors.c',
    'embed/lib/display.c',
    'embed/lib/fonts/fonts.c',
    'embed/lib/fonts/font_bitmap.c',
    'embed/extmod/moddetahardui/moddetahardui.c',
    'vendor/micropython/lib/uzlib/adler32.c',
    'vendor/micropython/lib/uzlib/crc32.c',
    'vendor/micropython/lib/uzlib/tinflate.c',
]

CPPDEFINES_MOD += [
    'detahard_UI2',
]

if detahard_MODEL not in ('1', ):
    CPPDEFINES_MOD += [
        'FANCY_FATAL_ERROR',
    ]

# moddetahardutils
SOURCE_MOD += [
    'embed/extmod/moddetahardutils/moddetahardutils.c',
]

# rust mods
SOURCE_MOD += [
    'embed/extmod/rustmods/moddetahardproto.c',
    'embed/extmod/rustmods/moddetahardui2.c',
]

# modutime
SOURCE_MOD += [
    'embed/extmod/modutime.c',
]

SOURCE_MICROPYTHON = [
    'vendor/micropython/extmod/modubinascii.c',
    'vendor/micropython/extmod/moductypes.c',
    'vendor/micropython/extmod/moduheapq.c',
    'vendor/micropython/extmod/modutimeq.c',
    'vendor/micropython/extmod/utime_mphal.c',
    'vendor/micropython/shared/libc/abort_.c',
    'vendor/micropython/shared/libc/printf.c',
    'vendor/micropython/shared/runtime/gchelper_m3.s',
    'vendor/micropython/shared/runtime/gchelper_native.c',
    'vendor/micropython/shared/runtime/interrupt_char.c',
    'vendor/micropython/shared/runtime/pyexec.c',
    'vendor/micropython/shared/runtime/stdout_helpers.c',
    'vendor/micropython/shared/timeutils/timeutils.c',
    'vendor/micropython/ports/stm32/gccollect.c',
    'vendor/micropython/ports/stm32/pendsv.c',
    'vendor/micropython/py/argcheck.c',
    'vendor/micropython/py/asmarm.c',
    'vendor/micropython/py/asmbase.c',
    'vendor/micropython/py/asmthumb.c',
    'vendor/micropython/py/asmx64.c',
    'vendor/micropython/py/asmx86.c',
    'vendor/micropython/py/asmxtensa.c',
    'vendor/micropython/py/bc.c',
    'vendor/micropython/py/binary.c',
    'vendor/micropython/py/builtinevex.c',
    'vendor/micropython/py/builtinhelp.c',
    'vendor/micropython/py/builtinimport.c',
    'vendor/micropython/py/compile.c',
    'vendor/micropython/py/emitbc.c',
    'vendor/micropython/py/emitcommon.c',
    'vendor/micropython/py/emitglue.c',
    'vendor/micropython/py/emitinlinethumb.c',
    'vendor/micropython/py/emitinlinextensa.c',
    'vendor/micropython/py/formatfloat.c',
    'vendor/micropython/py/frozenmod.c',
    'vendor/micropython/py/lexer.c',
    'vendor/micropython/py/malloc.c',
    'vendor/micropython/py/map.c',
    'vendor/micropython/py/modarray.c',
    'vendor/micropython/py/modbuiltins.c',
    'vendor/micropython/py/modgc.c',
    'vendor/micropython/py/modmath.c',
    'vendor/micropython/py/modmicropython.c',
    'vendor/micropython/py/modstruct.c',
    'vendor/micropython/py/modsys.c',
    'vendor/micropython/py/mpprint.c',
    'vendor/micropython/py/mpstate.c',
    'vendor/micropython/py/mpz.c',
    'vendor/micropython/py/nativeglue.c',
    'vendor/micropython/py/obj.c',
    'vendor/micropython/py/objarray.c',
    'vendor/micropython/py/objattrtuple.c',
    'vendor/micropython/py/objbool.c',
    'vendor/micropython/py/objboundmeth.c',
    'vendor/micropython/py/objcell.c',
    'vendor/micropython/py/objclosure.c',
    'vendor/micropython/py/objcomplex.c',
    'vendor/micropython/py/objdict.c',
    'vendor/micropython/py/objenumerate.c',
    'vendor/micropython/py/objexcept.c',
    'vendor/micropython/py/objfilter.c',
    'vendor/micropython/py/objfloat.c',
    'vendor/micropython/py/objfun.c',
    'vendor/micropython/py/objgenerator.c',
    'vendor/micropython/py/objgetitemiter.c',
    'vendor/micropython/py/objint.c',
    'vendor/micropython/py/objint_longlong.c',
    'vendor/micropython/py/objint_mpz.c',
    'vendor/micropython/py/objlist.c',
    'vendor/micropython/py/objmap.c',
    'vendor/micropython/py/objmodule.c',
    'vendor/micropython/py/objnamedtuple.c',
    'vendor/micropython/py/objnone.c',
    'vendor/micropython/py/objobject.c',
    'vendor/micropython/py/objpolyiter.c',
    'vendor/micropython/py/objproperty.c',
    'vendor/micropython/py/objrange.c',
    'vendor/micropython/py/objreversed.c',
    'vendor/micropython/py/objset.c',
    'vendor/micropython/py/objsingleton.c',
    'vendor/micropython/py/objslice.c',
    'vendor/micropython/py/objstr.c',
    'vendor/micropython/py/objstringio.c',
    'vendor/micropython/py/objstrunicode.c',
    'vendor/micropython/py/objtuple.c',
    'vendor/micropython/py/objtype.c',
    'vendor/micropython/py/objzip.c',
    'vendor/micropython/py/opmethods.c',
    'vendor/micropython/py/pairheap.c',
    'vendor/micropython/py/parse.c',
    'vendor/micropython/py/parsenum.c',
    'vendor/micropython/py/parsenumbase.c',
    'vendor/micropython/py/persistentcode.c',
    'vendor/micropython/py/qstr.c',
    'vendor/micropython/py/reader.c',
    'vendor/micropython/py/repl.c',
    'vendor/micropython/py/runtime.c',
    'vendor/micropython/py/runtime_utils.c',
    'vendor/micropython/py/scheduler.c',
    'vendor/micropython/py/scope.c',
    'vendor/micropython/py/sequence.c',
    'vendor/micropython/py/showbc.c',
    'vendor/micropython/py/smallint.c',
    'vendor/micropython/py/stackctrl.c',
    'vendor/micropython/py/stream.c',
    'vendor/micropython/py/unicode.c',
    'vendor/micropython/py/vstr.c',
    'vendor/micropython/py/warning.c',
]

SOURCE_MICROPYTHON_SPEED = [
    'vendor/micropython/py/gc.c',
    'vendor/micropython/py/pystack.c',
    'vendor/micropython/py/vm.c',
]

SOURCE_STMHAL = [
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_cortex.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_dma.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_flash.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_flash_ex.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_gpio.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_i2c.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_pcd.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_pcd_ex.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_pwr.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_rcc.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_sd.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_spi.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_sram.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_tim.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_tim_ex.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_ll_fmc.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_ll_sdmmc.c',
    'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_ll_usb.c',
]

SOURCE_FIRMWARE = [
    'embed/firmware/bl_check.c',
    'embed/firmware/delay.c',
    'embed/firmware/header.S',
    'embed/firmware/main.c',
    'embed/firmware/mphalport.c',
    'embed/firmware/nlrthumb.c',
    'embed/firmware/startup.S',
]

SOURCE_detahardHAL = [
    'embed/detahardhal/board_capabilities.c',
    'embed/detahardhal/common.c',
    'embed/detahardhal/dma.c',
    'embed/detahardhal/image.c',
    'embed/detahardhal/flash.c',
    'embed/detahardhal/mini_printf.c',
    'embed/detahardhal/mpu.c',
    'embed/detahardhal/random_delays.c',
    'embed/detahardhal/rng.c',
    'embed/detahardhal/stm32.c',
    'embed/detahardhal/systick.c',
    'embed/detahardhal/usb.c',
    'embed/detahardhal/usbd_conf.c',
    'embed/detahardhal/usbd_core.c',
    'embed/detahardhal/usbd_ctlreq.c',
    'embed/detahardhal/usbd_ioreq.c',
    'embed/detahardhal/util.s',
    'embed/detahardhal/vectortable.s',
]


if DMA2D:
    CPPDEFINES_MOD += [
        'USE_DMA2D',
    ]
    SOURCE_detahardHAL += [
        'embed/detahardhal/dma2d.c',
    ]
    SOURCE_STMHAL += [
        'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Src/stm32f4xx_hal_dma2d.c',
    ]

CPPDEFINES_MOD += ['USE_SVC_SHUTDOWN']

if FEATURE_FLAGS["RDI"]:
    CPPDEFINES_MOD += ['RDI']

if FEATURE_FLAGS["SYSTEM_VIEW"]:
    SOURCE_FIRMWARE += [
        'embed/segger/SEGGER/SEGGER_SYSVIEW_Config_NoOS.c',
        'embed/segger/SEGGER/SEGGER_SYSVIEW.c',
        'embed/segger/SEGGER/SEGGER_RTT.c',
        'embed/segger/SEGGER/SEGGER_RTT_ASM_ARMv7M.S',
        'embed/segger/SEGGER/Syscalls/SEGGER_RTT_Syscalls_GCC.c',
        'embed/firmware/systemview.c',
    ]
    CPPPATH_MOD += [
        'embed/segger/SEGGER/',
        'embed/segger/Config/',
    ]
    CPPDEFINES_MOD += ['SYSTEM_VIEW']
    CCFLAGS_MOD += '-DSYSTEM_VIEW '

# fonts
tools.add_font('NORMAL', FONT_NORMAL, CPPDEFINES_MOD, SOURCE_MOD)
tools.add_font('BOLD', FONT_BOLD, CPPDEFINES_MOD, SOURCE_MOD)
tools.add_font('DEMIBOLD', FONT_DEMIBOLD, CPPDEFINES_MOD, SOURCE_MOD)
tools.add_font('MONO', FONT_MONO, CPPDEFINES_MOD, SOURCE_MOD)

SOURCE_QSTR = SOURCE_MOD + SOURCE_MICROPYTHON + SOURCE_MICROPYTHON_SPEED

env = Environment(ENV=os.environ, CFLAGS=f"{ARGUMENTS.get('CFLAGS', '')} -DPRODUCTION={int(PRODUCTION)} -DPYOPT={PYOPT} -DBOOTLOADER_QA={int(BOOTLOADER_QA)} -DBITCOIN_ONLY={BITCOIN_ONLY}")

FEATURES_AVAILABLE = tools.configure_board(detahard_MODEL, FEATURES_WANTED, env, CPPDEFINES_MOD, SOURCE_detahardHAL)

env.Tool('micropython')

env.Replace(
    CAT='cat',
    DD='dd',
    CP='cp',
    SED='sed',
    AS='arm-none-eabi-as',
    AR='arm-none-eabi-ar',
    CC='arm-none-eabi-gcc',
    LINK='arm-none-eabi-gcc',
    SIZE='arm-none-eabi-size',
    STRIP='arm-none-eabi-strip',
    OBJCOPY='arm-none-eabi-objcopy', )

env.Replace(
    detahard_MODEL=detahard_MODEL,)

if detahard_MODEL in ('T', 'R'):
    CPU_ASFLAGS = '-mthumb -mcpu=cortex-m4 -mfloat-abi=hard -mfpu=fpv4-sp-d16'
    CPU_CCFLAGS = '-mthumb -mcpu=cortex-m4 -mfloat-abi=hard -mfpu=fpv4-sp-d16 -mtune=cortex-m4 '
    CPU_MODEL   = 'STM32F427xx'
    LD_VARIANT  = ''
    RUST_TARGET = 'thumbv7em-none-eabihf'
elif detahard_MODEL in ('1',):
    CPU_ASFLAGS = '-mthumb -mcpu=cortex-m3 -mfloat-abi=soft'
    CPU_CCFLAGS = '-mthumb -mtune=cortex-m3 -mcpu=cortex-m3 -mfloat-abi=soft '
    CPU_MODEL   = 'STM32F405xx'
    LD_VARIANT  = '' if EVERYTHING else '_min'
    RUST_TARGET = 'thumbv7m-none-eabi'
else:
    raise ValueError('Unknown detahard model')

env.Replace(
    COPT=env.get('ENV').get('OPTIMIZE', '-Os'),
    CCFLAGS='$COPT '
    '-g3 '
    '-nostdlib '
    '-std=gnu99 -Wall -Werror -Wdouble-promotion -Wpointer-arith -Wno-missing-braces -fno-common '
    '-fsingle-precision-constant -fdata-sections -ffunction-sections '
    '-ffreestanding '
    '-fstack-protector-all '
    + CPU_CCFLAGS + CCFLAGS_MOD,
    CCFLAGS_QSTR='-DNO_QSTR -DN_X64 -DN_X86 -DN_THUMB',
    LINKFLAGS='-T embed/firmware/memory_${detahard_MODEL}%s.ld -Wl,--gc-sections  -Wl,--print-memory-usage -Wl,-Map=build/firmware/firmware.map -Wl,--warn-common' % LD_VARIANT,
    CPPPATH=[
        '.',
        'embed/rust',
        'embed/firmware',
        'embed/lib',
        'embed/detahardhal',
        'embed/extmod/moddetahardui',
        'vendor/micropython',
        'vendor/micropython/lib/stm32lib/STM32F4xx_HAL_Driver/Inc',
        'vendor/micropython/lib/stm32lib/CMSIS/STM32F4xx/Include',
        'vendor/micropython/lib/cmsis/inc',
    ] + CPPPATH_MOD,
    CPPDEFINES=[
        'FIRMWARE',
        'detahard_MODEL_'+detahard_MODEL,
        CPU_MODEL,
        'USE_HAL_DRIVER',
        ('STM32_HAL_H', '"<stm32f4xx.h>"'),
    ] + CPPDEFINES_MOD,
    ASFLAGS=CPU_ASFLAGS,
    ASPPFLAGS='$CFLAGS $CCFLAGS', )

env.Replace(
    HEADERTOOL='tools/headertool.py',
    PYTHON='python',
    MAKEQSTRDATA='$PYTHON vendor/micropython/py/makeqstrdata.py',
    MAKEVERSIONHDR='$PYTHON vendor/micropython/py/makeversionhdr.py',
    MAKEMODULEDEFS='$PYTHON vendor/micropython/py/makemoduledefs.py',
    MAKECMAKELISTS='$PYTHON tools/make_cmakelists.py',
    MPY_TOOL='$PYTHON vendor/micropython/tools/mpy-tool.py',
    MPY_CROSS='vendor/micropython/mpy-cross/mpy-cross -O' + PYOPT,
    PB2PY='$PYTHON ../common/protob/pb2py',
)

#
# Qstrings
#

PROTO_SOURCES_DIR = '../../../common/protob/'
PROTO_SOURCES = Glob(PROTO_SOURCES_DIR + '*.proto',
    exclude=[PROTO_SOURCES_DIR + 'messages-bootloader.proto']
)
qstr_protobuf = env.Command(
    target=[
        'genhdr/qstrdefs.protobuf.h',
    ],
    source=PROTO_SOURCES,
    action='$PB2PY $SOURCES --qstr-out ${TARGET} --bitcoin-only=%s' % BITCOIN_ONLY,
)

qstr_micropython = 'vendor/micropython/py/qstrdefs.h'

qstr_collected = env.CollectQstr(
    target='genhdr/qstrdefs.collected.h', source=SOURCE_QSTR)

qstr_preprocessed = env.PreprocessQstr(
    target='genhdr/qstrdefs.preprocessed.h',
    source=[qstr_micropython, qstr_protobuf, qstr_collected])

qstr_generated = env.GenerateQstrDefs(
    target='genhdr/qstrdefs.generated.h', source=qstr_preprocessed)

env.Ignore(qstr_collected, qstr_generated)

#
# Micropython module declarations
#

moduledefs_collected = env.CollectModules(
    target='genhdr/moduledefs.collected.h', source=SOURCE_QSTR)

hdr_moduledefs = env.Command(
    target='genhdr/moduledefs.h',
    source=moduledefs_collected,
    action='$MAKEMODULEDEFS $SOURCE > $TARGET', )

env.Ignore(moduledefs_collected, moduledefs_collected)
env.Ignore(moduledefs_collected, qstr_generated)
env.Ignore(moduledefs_collected, hdr_moduledefs)

#
# Micropython version
#

hdr_version = env.Command(
    target='genhdr/mpversion.h',
    source='',
    action='$MAKEVERSIONHDR $TARGET', )

env.Ignore(hdr_moduledefs, hdr_moduledefs)
env.Ignore(hdr_moduledefs, qstr_collected)
env.Ignore(hdr_moduledefs, qstr_preprocessed)
env.Ignore(hdr_moduledefs, qstr_generated)

#
# Frozen modules
#

if FROZEN:
    SOURCE_PY_DIR = 'src/'

    SOURCE_PY = Glob(SOURCE_PY_DIR + '*.py')
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/crypto/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/ui/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/ui/components/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/ui/components/common/*.py'))

    # UI layouts - common files and then model-specific. Exclude FIDO when BTC-only.
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/ui/layouts/*.py',
        exclude=[
            SOURCE_PY_DIR + 'detahard/ui/layouts/fido.py',
        ] if not EVERYTHING else []
    ))
    if detahard_MODEL in ('T',):
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/ui/layouts/tt_v2/*.py',
            exclude=[
                SOURCE_PY_DIR + 'detahard/ui/layouts/tt_v2/fido.py',
            ] if not EVERYTHING else []
        ))
    elif detahard_MODEL in ('1', 'R'):
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/ui/layouts/tr/__init__.py'))
    else:
        raise ValueError('Unknown detahard model')

    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/wire/*.py'))

    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'storage/*.py'))

    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/messages/__init__.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/*.py',
        exclude=[
            SOURCE_PY_DIR + 'detahard/enums/Binance*.py',
            SOURCE_PY_DIR + 'detahard/enums/Cardano*.py',
            SOURCE_PY_DIR + 'detahard/enums/DebugMonero*.py',
            SOURCE_PY_DIR + 'detahard/enums/Eos*.py',
            SOURCE_PY_DIR + 'detahard/enums/Ethereum*.py',
            SOURCE_PY_DIR + 'detahard/enums/Monero*.py',
            SOURCE_PY_DIR + 'detahard/enums/NEM*.py',
            SOURCE_PY_DIR + 'detahard/enums/Ripple*.py',
            SOURCE_PY_DIR + 'detahard/enums/Stellar*.py',
            SOURCE_PY_DIR + 'detahard/enums/Tezos*.py',
            SOURCE_PY_DIR + 'detahard/enums/Zcash*.py',
        ])
    )

    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/common/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/common/*/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/debug/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/homescreen/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/management/*.py',
        exclude=[
            SOURCE_PY_DIR + 'apps/management/sd_protect.py',
        ] if 'sd_card' not in FEATURES_AVAILABLE else [])
    )
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/management/*/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/misc/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/bitcoin/*.py'))
    SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/bitcoin/*/*.py',
        exclude=[
            SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/decred.py',
            SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/bitcoinlike.py',
            SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/zcash_v4.py',
        ])
    )

    if EVERYTHING:
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/binance/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/Binance*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/cardano/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/cardano/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/Cardano*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/eos/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/eos/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/Eos*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/ethereum/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/Ethereum*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/monero/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/monero/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/monero/*/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/DebugMonero*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/Monero*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/nem/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/nem/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/NEM*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/ripple/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/Ripple*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/stellar/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/stellar/*/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/Stellar*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/tezos/*.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/Tezos*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/zcash/*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/webauthn/*.py'))

        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/decred.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/bitcoinlike.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'apps/bitcoin/sign_tx/zcash_v4.py'))
        SOURCE_PY.extend(Glob(SOURCE_PY_DIR + 'detahard/enums/Zcash*.py'))

    source_mpy = env.FrozenModule(source=SOURCE_PY, source_dir=SOURCE_PY_DIR, bitcoin_only=BITCOIN_ONLY)

    source_mpyc = env.FrozenCFile(
        target='frozen_mpy.c', source=source_mpy, qstr_header=qstr_preprocessed)

    env.Depends(source_mpyc, qstr_generated)

#
# Rust library
#

protobuf_blobs = env.Command(
    target=[
        'rust/proto_enums.data',
        'rust/proto_msgs.data',
        'rust/proto_names.data',
        'rust/proto_wire.data',
    ],
    source=PROTO_SOURCES,
    action='$PB2PY --bitcoin-only=%s --blob-outdir ${TARGET.dir} $SOURCES --qstr-defs build/firmware/genhdr/qstrdefs.generated.h' % BITCOIN_ONLY,
)
env.Depends(protobuf_blobs, qstr_generated)

RUST_PROFILE = 'release'
RUST_LIB = 'detahard_lib'
RUST_LIBDIR = f'build/firmware/rust/{RUST_TARGET}/{RUST_PROFILE}'
RUST_LIBPATH = f'{RUST_LIBDIR}/lib{RUST_LIB}.a'

def cargo_build():
    # Determine the profile build flags.
    if RUST_PROFILE == 'release':
        profile = '--release'
    else:
        profile = ''

    # T1 does not have its own Rust feature, it shares it with TR
    model_feature = 'model_tr' if detahard_MODEL == '1' else f'model_t{detahard_MODEL.lower()}'
    features = ['micropython', 'protobuf', model_feature]
    if BITCOIN_ONLY == '1':
        features.append('bitcoin_only')
    features.append('ui')
    if PYOPT == '0':
        features.append('ui_debug')
    if DMA2D:
        features.append('dma2d')

    features.extend(FEATURES_AVAILABLE)

    cargo_opts = [
        f'--target={RUST_TARGET}',
        f'--target-dir=../../build/firmware/rust',
        '--no-default-features',
        '--features ' + ','.join(features),
        '-Z build-std=core',
        '-Z build-std-features=panic_immediate_abort',
    ]

    env.get('ENV')['detahard_MODEL'] = detahard_MODEL

    return f'cd embed/rust; cargo build {profile} ' + ' '.join(cargo_opts)

rust = env.Command(
    target=RUST_LIBPATH,
    source='',
    action=cargo_build(), )
env.Depends(rust, protobuf_blobs)

env.Append(LINKFLAGS=f' -L{RUST_LIBDIR}')
env.Append(LINKFLAGS=f' -l{RUST_LIB}')

#
# Program objects
#

source_files = SOURCE_MOD + SOURCE_FIRMWARE + SOURCE_MICROPYTHON + SOURCE_MICROPYTHON_SPEED + SOURCE_STMHAL + SOURCE_detahardHAL
obj_program = []
obj_program.extend(env.Object(source=SOURCE_MOD))
if FEATURE_FLAGS["SECP256K1_ZKP"]:
    obj_program.extend(env.Object(source=SOURCE_MOD_SECP256K1_ZKP, CCFLAGS='$CCFLAGS -Wno-unused-function'))
    source_files.extend(SOURCE_MOD_SECP256K1_ZKP)
obj_program.extend(env.Object(source=SOURCE_FIRMWARE))
obj_program.extend(env.Object(source=SOURCE_MICROPYTHON))
obj_program.extend(env.Object(source=SOURCE_MICROPYTHON_SPEED, COPT='-O3'))
obj_program.extend(env.Object(source=SOURCE_STMHAL))
obj_program.extend(env.Object(source=SOURCE_detahardHAL))
if FROZEN:
    obj_program.extend(env.Object(source=source_mpyc))

env.Replace(
    ALLSOURCES=source_files,
    ALLDEFS=tools.get_defs_for_cmake(env['CPPDEFINES'] + [f"PRODUCTION={int(PRODUCTION)}", f"BOOTLOADER_QA={int(BOOTLOADER_QA)}", f"PYOPT={PYOPT}", f"BITCOIN_ONLY={BITCOIN_ONLY}"]))


cmake_gen = env.Command(
    target='CMakeLists.txt',
    source='',
    action='$MAKECMAKELISTS --sources $ALLSOURCES --dirs $CPPPATH --defs $ALLDEFS',
)


MODEL_IDENTIFIER = tools.get_model_identifier(detahard_MODEL)
if BOOTLOADER_QA:
    VENDORHEADER = 'embed/vendorheader/vendorheader_qa_DO_NOT_SIGN_signed_dev.bin'
    BOOTLOADER_SUFFIX = MODEL_IDENTIFIER + '_qa'
elif PRODUCTION:
    VENDORHEADER = 'embed/vendorheader/vendorheader_satoshilabs_signed_prod.bin'
    BOOTLOADER_SUFFIX = MODEL_IDENTIFIER
else:
    VENDORHEADER = 'embed/vendorheader/vendorheader_unsafe_signed_prod.bin'
    BOOTLOADER_SUFFIX = MODEL_IDENTIFIER

obj_program.extend(
    env.Command(
        target='embed/firmware/vendorheader.o',
        source=VENDORHEADER,
        action='$OBJCOPY -I binary -O elf32-littlearm -B arm'
        ' --rename-section .data=.vendorheader,alloc,load,readonly,contents'
        ' $SOURCE $TARGET', ))

obj_program.extend(
    env.Command(
        target='embed/firmware/bootloaders/bootloader.o',
        source=f'embed/firmware/bootloaders/bootloader_{BOOTLOADER_SUFFIX}.bin',
        action='$OBJCOPY -I binary -O elf32-littlearm -B arm'
        ' --rename-section .data=.bootloader'
        f' --redefine-sym _binary_embed_firmware_bootloaders_bootloader_{BOOTLOADER_SUFFIX}_bin_start=_binary_embed_firmware_bootloader_bin_start'
        f' --redefine-sym _binary_embed_firmware_bootloaders_bootloader_{BOOTLOADER_SUFFIX}_bin_end=_binary_embed_firmware_bootloader_bin_end'
        f' --redefine-sym _binary_embed_firmware_bootloaders_bootloader_{BOOTLOADER_SUFFIX}_bin_size=_binary_embed_firmware_bootloader_bin_size'
        ' $SOURCE $TARGET', ))

env.Depends(obj_program, qstr_generated)

program_elf = env.Command(
    target='firmware.elf',
    source=obj_program,
    action=
    '$LINK -o $TARGET $CCFLAGS $CFLAGS $SOURCES $LINKFLAGS -lc_nano -lm -lgcc',
)

if CMAKELISTS != 0:
    env.Depends(program_elf, cmake_gen)
env.Depends(program_elf, rust)

BINARY_NAME = f"build/firmware/firmware-{tools.get_model_identifier(detahard_MODEL)}"
if not EVERYTHING:
    BINARY_NAME += "-btconly"
BINARY_NAME += "-" + tools.get_version('embed/firmware/version.h')
BINARY_NAME += "-" + tools.get_git_revision_short_hash()
BINARY_NAME += "-dirty" if tools.get_git_modified() else ""
BINARY_NAME += ".bin"


if detahard_MODEL in ('T', 'R'):
    action_bin=[
        '$OBJCOPY -O binary -j .vendorheader -j .header -j .flash -j .data --pad-to 0x08100000 $SOURCE ${TARGET}.p1',
        '$OBJCOPY -O binary -j .flash2 $SOURCE ${TARGET}.p2',
        '$CAT ${TARGET}.p1 ${TARGET}.p2 > $TARGET',
        '$HEADERTOOL -h $TARGET ' + ('-D' if not PRODUCTION else ''),
        '$DD if=$TARGET of=${TARGET}.p1 skip=0 bs=128k count=6',
        '$CP $TARGET ' + BINARY_NAME,
    ]
elif detahard_MODEL in ('1',):
    action_bin=[
        '$OBJCOPY -O binary -j .header -j .flash -j .data $SOURCE $TARGET',
        '../legacy/bootloader/firmware_sign.py -f $TARGET',
        '$CP $TARGET ' + BINARY_NAME,
    ]
else:
    raise ValueError('Unknown detahard model')

program_bin = env.Command(
    target='firmware.bin',
    source=program_elf,
    action=action_bin,
)
